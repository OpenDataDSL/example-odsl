print "**************************************************************"
print "*********************** Object Test **************************"
print "**************************************************************"

use test

Widget = versioned type
    name as String()
    price as Number()
end
save ${type:Widget}

Cable = versioned type extends Widget
    length as Number()
end
save ${type:Cable}

Switch = versioned type extends Widget
    ports as Number()
end
save ${type:Switch}

Cat5ACable = object as Cable
    name = "Cat 5A Ethernet cable"
    price = 1.25
    length = 5
end
save ${object:Cat5ACable}

Cat6Cable = object as Cable
    name = "Cat 6 Ethernet cable"
    price = 1.55
    length = 5
end
save ${object:Cat6Cable}

AZ24Switch = object as Switch
    name = "AZ 24 port switch"
    price = 24.95
    ports = 24
end
save ${object:AZ24Switch}

widgets = find ${object:"Widget"}
assertEquals(3, widgets.size, "Should be 3 widgets")
cables = find ${object:"Cable"}
assertEquals(2, cables.size, "Should be 2 cables")
switches = find ${object:"Switch"}
assertEquals(1, switches.size, "Should be 1 switches")

w2 = ${object:"Widget/Cable/Cat5ACable"}
assertType(w2, "Widget")

w1 = ${object:"Widget/Cable/Cat5ACable":1}
assertType(w1, "Widget")

c1 = ${object:"Cable/Cat5ACable":1}
assertType(c1, "Cable")

tag ${object:"Widget/Cable/Cat6Cable":1} as PROD

pw = ${object:"Widget/Cable/Cat6Cable":PROD}
assertType(pw, "Widget")

print "Object test complete!"